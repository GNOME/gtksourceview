dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.53)

# Initialization and versioning
AC_INIT(gtksourceview, 0.5.0, http://bugzilla.gnome.org/enter_bug.cgi?product=gtksourceview)
AC_CONFIG_SRCDIR([gtksourceview/gtksourcebuffer.h])
AM_CONFIG_HEADER(config.h)

AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)

dnl make sure we keep ACLOCAL_FLAGS around for maintainer builds to work
AC_SUBST(ACLOCAL_AMFLAGS, "$ACLOCAL_FLAGS")

AM_MAINTAINER_MODE

# Check for programs
AC_PROG_CC
AC_PROG_INSTALL
AM_DISABLE_STATIC
AM_PROG_LIBTOOL
# FIXME: update required version when there's a new intltool release
AC_PROG_INTLTOOL(0.26)

# Check for headers and functions
AC_HEADER_STDC
AC_CHECK_HEADERS([stdlib.h string.h])
AC_CHECK_FUNCS([memset])

# Dependencies
GTK_REQUIRED_VERSION=2.2.0
GNOME_VFS_REQUIRED_VERSION=2.2.0
LIBXML_REQUIRED_VERSION=2.5.0
GNOME_PRINT_REQUIRED_VERSION=2.2.0

AC_SUBST(GTK_REQUIRED_VERSION)
AC_SUBST(GNOME_VFS_REQUIRED_VERSION)
AC_SUBST(LIBXML_REQUIRED_VERSION)
AC_SUBST(GNOME_PRINT_REQUIRED_VERSION)

# Pull glib-genmarshal & co.
AC_PATH_PROG(GLIB_GENMARSHAL, glib-genmarshal)
AC_PATH_PROG(GLIB_MKENUMS, glib-mkenums)

# Environment
GNOME_COMMON_INIT
GNOME_COMPILE_WARNINGS(error)

PKG_CHECK_MODULES(DEP,
	gtk+-2.0 >= $GTK_REQUIRED_VERSION
	libxml-2.0 >= $LIBXML_REQUIRED_VERSION
	libgnomeprint-2.2 >= $GNOME_PRINT_REQUIRED_VERSION)
AC_SUBST(DEP_CFLAGS)
AC_SUBST(DEP_LIBS)

# Check for GNU regex functions
AH_TEMPLATE(NATIVE_GNU_REGEX, [Define if the C library implements the GNU regular expressions API])
BUILD_GNU_REGEX=false
AC_CHECK_FUNCS([re_compile_pattern re_compile_fastmap re_search re_match], [
	AC_DEFINE(NATIVE_GNU_REGEX)
], [
	BUILD_GNU_REGEX=true
])
AM_CONDITIONAL(BUILD_GNU_REGEX,$BUILD_GNU_REGEX)

# Do we build optional tests? (they require gnome-vfs)
AC_ARG_ENABLE(build_tests,AC_HELP_STRING([--disable-build-tests],
					     [disable building of the test programs]),
[case "${enableval}" in
  yes) build_tests=true ;;
  no)  build_tests=false ;;
  *) AC_MSG_ERROR(bad value ${enableval} for --disable-build-tests) ;;
esac],[build_tests=true])
AM_CONDITIONAL(BUILD_TESTS, test x$build_tests = xtrue)

if test x$build_tests = xtrue; then
	PKG_CHECK_MODULES(TESTS,
		gnome-vfs-2.0 >= $GNOME_VFS_REQUIRED_VERSION
		libgnomeprintui-2.2 >= $GNOME_PRINT_REQUIRED_VERSION)
	AC_SUBST(TESTS_CFLAGS)
	AC_SUBST(TESTS_LIBS)
fi

# I18N stuff
ALL_LINGUAS="az be ca cs cy da de el es et fr he it ja ko mk ml ms nl no pl pt pt_BR ru sk sr sr@Latn sv tr vi zh_CN zh_TW"
GETTEXT_PACKAGE=gtksourceview-1.0
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [Package name for gettext])

AM_GLIB_GNU_GETTEXT
AM_GLIB_DEFINE_LOCALEDIR(LOCALEDIR)

# gtk-doc
GTK_DOC_CHECK([1.0])

# Output files
AC_CONFIG_FILES([
gtksourceview-1.0.pc
Makefile
po/Makefile.in
gtksourceview/Makefile
gtksourceview/gnu-regex/Makefile
gtksourceview/language-specs/Makefile
docs/Makefile
docs/reference/Makefile
tests/Makefile
])

AC_OUTPUT
